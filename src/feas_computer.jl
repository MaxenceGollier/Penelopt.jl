function compute_θ!(solver::L2PenaltySolver{T}) where {T}
  # Computes a model decrease for the feasbility problem minₓ ‖c(x)‖₂

  ## Retrieve workspace
  r2n_solver, r2n_stats = solver.subsolver, solver.substats
    
  ms_problem, ms_solver, ms_stats = r2n_solver.subpb, r2n_solver.subsolver, r2n_solver.substats
  ls_workspace = ms_solver.workspace
  u1, x1 = ms_solver.u1, ms_solver.x1
  nlp, ψ = ms_problem.model, ms_problem.h
  n, m = nlp.meta.nvar, length(ψ.b)

  # Step 1: Compute
  # ( I     Jᵀ )( s ) = - ( 0 )
  # ( J    -αI )( y ) = - ( c )
  @. u1[1:n] = 0
  @. u1[(n+1):(n+m)] = - ψ.b

  σ, α = one(T), sqrt(eps(T))
  update_workspace!(
    ls_workspace,
    ψ.A,
    σ,
    α,
  )

  solve_system!(ls_workspace, u1)
  get_solution!(x1, ls_workspace)
  s, y = view(x1, 1:n), view(x1, (n+1):(n+m))

  # Step 2: Compute θ = (‖s‖₂/‖y‖₂)
  norm_y = norm(y, 2)
  θ = iszero(norm_y) ? zero(T) : norm(s, 2) / norm_y

  # Set factorization counters
  set_solver_specific!(r2n_stats, :n_fact, get_n_fact(ls_workspace))

  return θ
end

function kkt_primal_feas!(solver::L2PenaltySolver{T}) where {T}
  return norm(solver.subsolver.subpb.h.b, Inf)
end

function compute_least_square_multipliers!(solver::L2PenaltySolver{T}) where {T}

  ## Retrieve workspace
  r2n_solver, r2n_stats = solver.subsolver, solver.substats
    
  ms_problem, ms_solver, ms_stats = r2n_solver.subpb, r2n_solver.subsolver, r2n_solver.substats
  ls_workspace = ms_solver.workspace
  u1, x1 = ms_solver.u1, ms_solver.x1
  nlp, ψ = ms_problem.model, ms_problem.h
  n, m = nlp.meta.nvar, length(ψ.b)

  # Step 1: Compute
  # ( I     Jᵀ )( s ) = - ( ∇f )
  # ( J     0I )( y ) = - ( 0  )
  @. u1[1:n] = - solver.∇fk
  @. u1[(n+1):(n+m)] = 0

  σ, α = one(T), zero(T)
  update_workspace!(
    ls_workspace,
    ψ.A,
    σ,
    α,
  )

  solve_system!(ls_workspace, u1)
  get_solution!(x1, ls_workspace)

  # Step 2: Check Jacobian full row rank and recompute if necessary
  status = get_status(ls_workspace)
  if status != :success
    α = sqrt(eps(T))
    update_workspace!(
      ls_workspace,
      ψ.A,
      σ,
      α,
    )
    solve_system!(ls_workspace, u1)
    get_solution!(x1, ls_workspace)
  end

  # Step 3: Extract the solution
  s, y = view(x1, 1:n), view(x1, (n+1):(n+m))
  solver.y .= y

  # Set factorization counters
  set_solver_specific!(r2n_stats, :n_fact, get_n_fact(ls_workspace))
end

function update_constraint_multipliers!(solver::L2PenaltySolver{T}) where {T}
  σ =
    isa(solver.subsolver, PenaltyR2NSolver) ?
    solver.substats.solver_specific[:sigma_cauchy] : solver.substats.solver_specific[:sigma]
  @. solver.y = solver.subsolver.subpb.h.q * σ
end

function kkt_dual_feas!(solver::L2PenaltySolver{T}) where {T}
  return solver.substats.dual_feas
end

function least_square_dual_feas!(solver::L2PenaltySolver{T}) where {T}
  dual_res, y = solver.dual_res, solver.y
  g, J = solver.∇fk, solver.subsolver.subpb.h.A

  dual_res .= g
  mul!(dual_res, J', y, one(T), -one(T))
  return norm(dual_res, Inf)
end
