mutable struct PenaltyMUMPSWorkspace{
  WP<:Mumps,
  K2<:AbstractMatrix,
  V<:AbstractVector,
  T<:Real,
} <: AbstractMUMPSWorkspace
  M::WP
  H::K2
  x::V
  σ::T
  n::Int
  m::Int
  status::Symbol
  factorized::Bool
  _n_fact::Int
end

function get_H(
  solver_workspace::PenaltyMUMPSWorkspace{WP,K2},
) where {T,M,WP,K2<:Symmetric{T,M}}
  return solver_workspace.H.data
end

function get_H(solver_workspace::PenaltyMUMPSWorkspace{WP,K2}) where {WP,K2<:CompactBFGSK2}
  return solver_workspace.H.H.data
end

function construct_mumps_workspace(
  H::M,
  u1::V,
  n,
  m,
) where {T,V<:AbstractVector{T},M<:Symmetric}
  # Set params : TODO
  cntl = T == Float64 ? default_cntl64 : default_cntl32
  icntl = default_icntl

  ## Set Parameters
  cntl[2] = eps(T) # Tolerance for iterative refinement

  # Deactivate Logging
  icntl[2], icntl[3], icntl[4] = 0, 0, 0

  # Deactivate permutation/scaling
  icntl[6], icntl[7], icntl[8] = 0, 0, 0

  # Max number of iterative refinement steps
  icntl[10] = 10

  # ICNTL(11): error analysis
  # 2: Main statistics (recommended)
  icntl[11] = 2

  # ICNTL(24) controls the detection of “null pivot rows”.
  # 1: Null pivot row detection.
  icntl[24] = 1

  # MUMPS Documentation - Definite matrices (SYM=1).
  # Remark for symmetric matrices (SYM=1). When SYM=1 is indicated by the user, an LDLT
  # factorization (in opposition to Cholesky factorization which requires positive diagonal pivots) of matrix
  # A is performed internally by the package, and numerical pivoting is switched off. Therefore, this
  # setting works for classes of matrices more general than positive definite matrices, including matrices with
  # negative pivots. 
  S = Mumps{T}(mumps_definite, icntl, cntl)

  # Associate the row, cols and vals of the mumps structure with those of H.
  irn, jcn, a = H.data.rows, H.data.cols, H.data.vals
  S.irn, S.jcn, S.a = pointer.((irn, jcn, a))
  S.n = m+n
  S.nnz = length(irn)
  S._irn_gc_haven = irn
  S._jcn_gc_haven = jcn
  S._a_gc_haven = a

  # Associate the size and number of the right hand side
  x = similar(u1)
  S.lrhs = n + m
  S.nrhs = 1
  S.rhs = pointer(x)
  S._y_gc_haven = x

  return PenaltyMUMPSWorkspace(S, H, x, zero(T), n, m, :uninitialized, false, 0)
end

function construct_mumps_workspace(
  H::M,
  u1::V,
  n,
  m,
) where {T,V<:AbstractVector{T},M<:CompactBFGSK2}
  # Set params : TODO
  cntl = T == Float64 ? default_cntl64 : default_cntl32
  icntl = default_icntl

  ## Set Parameters
  cntl[2] = eps(T) # Tolerance for iterative refinement

  # Deactivate Logging
  icntl[2], icntl[3], icntl[4] = 0, 0, 0

  # Deactivate permutation/scaling
  icntl[6], icntl[7], icntl[8] = 0, 0, 0

  # Max number of iterative refinement steps
  icntl[10] = 10

  S = Mumps{T}(mumps_definite, icntl, cntl)

  # Associate the row, cols and vals of the mumps structure with those of H.
  irn, jcn, a = H.H.data.rows, H.H.data.cols, H.H.data.vals
  S.irn, S.jcn, S.a = pointer.((irn, jcn, a))
  S.n = m+n
  S.nnz = length(irn)
  S._irn_gc_haven = irn
  S._jcn_gc_haven = jcn
  S._a_gc_haven = a

  # Associate the size and number of the right hand side
  x = similar(u1)
  S.lrhs = n + m
  S.nrhs = 1
  S.rhs = pointer(x)
  S._y_gc_haven = x

  return PenaltyMUMPSWorkspace(S, H, x, zero(T), n, m, :uninitialized, false, 0)
end

function update_workspace!(
  solver_workspace::PenaltyMUMPSWorkspace,
  B::M,
  A,
  σ,
  α,
) where {M<:SparseMatrixCOO}
  n, m = solver_workspace.n, solver_workspace.m
  nnz_B, nnz_A = length(B.vals), length(A.vals)

  H = get_H(solver_workspace)

  H.vals[1:nnz_B] .= B.vals
  H.vals[(nnz_B+1):(nnz_B+nnz_A)] .= A.vals
  H.vals[(nnz_B+nnz_A+1):(nnz_B+nnz_A+n)] .= σ
  H.vals[(nnz_B+nnz_A+n+1):(nnz_B+nnz_A+n+m)] .= -α

  solver_workspace.σ = σ
  solver_workspace.factorized = false
end

function update_workspace!(
  solver_workspace::PenaltyMUMPSWorkspace,
  B::M,
  A,
  σ,
  α,
) where {M<:CompactBFGS}
  n, m = solver_workspace.n, solver_workspace.m
  nnz_A = length(A.vals)

  H = get_H(solver_workspace)

  H.vals[1:nnz_A] .= A.vals
  H.vals[(nnz_A+1):(nnz_A+n)] .= σ + B.ξ
  H.vals[(nnz_A+n+1):(nnz_A+n+m)] .= -α

  solver_workspace.σ = σ
  solver_workspace.factorized = false
end

function set_dual_inertia!(solver_workspace::PenaltyMUMPSWorkspace, α)
  n, m = solver_workspace.n, solver_workspace.m
  H = get_H(solver_workspace)
  H.vals[(end-m+1):end] .= -α
  solver_workspace.factorized = false
end

function set_primal_inertia!(solver_workspace::PenaltyMUMPSWorkspace, σ)
  n, m = solver_workspace.n, solver_workspace.m
  H = get_H(solver_workspace)
  H.vals[(end-m-n+1):(end-m)] .= σ
  solver_workspace.σ = σ
  solver_workspace.factorized = false
end

function solve_system!(
  workspace::PenaltyMUMPSWorkspace{WP,K2},
  u::V,
) where {V<:AbstractVector,WP,K2}
  workspace.status = :success
  mumps, H = workspace.M, workspace.H

  if !workspace.factorized
    job = mumps.job
    mumps.job = MUMPS.INITIALIZE
    factorize!(mumps)
    workspace._n_fact += 1

    k, max_iter = 0, 5
    # MUMPS Documentation - infog(1) = -9
    # The main internal real/complex workarray S is too small. If INFO(2) is positive, then the number
    # of entries that are missing in S at the moment when the error is raised is available in INFO(2).
    # If INFO(2) is negative, then its absolute value should be multiplied by 1 million. If an error –9
    # occurs, the user should increase the value of ICNTL(14) before calling the factorization (JOB=
    # 2) again, except if LWK USER is provided LWK USER should be increased.
    while mumps.infog[1] == -9 && k < max_iter
      MUMPS.set_icntl!(mumps, 14, mumps.icntl[14] * 2)
      mumps.job = MUMPS.FACTOR
      factorize!(mumps)
      workspace._n_fact += 1
      k = k + 1
    end

    # MUMPS infog(1): a negative value is an error in the factorization.
    if mumps.infog[1] < 0
      workspace.status = :failed
      return
    else
      workspace.factorized = true
    end
  end

  workspace.x .= u
  MUMPS.mumps_solve!(workspace.x, mumps; rhs_changed = true)

  # MUMPS infog(1): a negative value is an error in the factorization.
  if any(isnan, workspace.x) || mumps.infog[1] < 0
    workspace.status = :failed
  end

  if mumps.rinfog[6] > sqrt(eps(eltype(workspace.x))) ||
     mumps.rinfog[7] > sqrt(eps(eltype(workspace.x))) ||
     mumps.rinfog[8] > sqrt(eps(eltype(workspace.x)))
    workspace.status = :failed

    # Switch to symmetric indefinite factorization
    mumps_switch_to_indefinite!(workspace)
  end

  return
end

function solve_system!(
  workspace::PenaltyMUMPSWorkspace{WP,K2},
  u::V,
) where {V<:AbstractVector,WP,K2<:CompactBFGSK2}
  workspace.status = :success
  H = workspace.H
  B = workspace.H.B
  mumps = workspace.M
  n, m = workspace.n, workspace.m
  p = min(B._insert - 1, B._mem)
  x1, x2, x3, y1, y2 = H.x1, H.x2, H.x3, H.y1, H.y2
  Z1, Z2 = H.Z1, H.Z2

  Uk = @view B.Uk[:, 1:p]
  Vk = @view B.Vk[:, 1:p]

  # Step 0: Write
  # [B  Aᵀ] = [σI+ξI  Aᵀ] + [-U V]([U V])ᵀ
  # [A -αI] = [A     -αI] + [ 0 0]([0 0])
  # Hence,
  # [B  Aᵀ] = [σI+ξI  Aᵀ] + EFᵀ
  # [A -αI] = [A     -αI] + EFᵀ

  # Step 1: Factorize
  # [σI+ξI  Aᵀ]
  # [A     -αI]

  if !workspace.factorized
    job = mumps.job
    mumps.job = MUMPS.INITIALIZE
    factorize!(mumps)
    workspace._n_fact += 1

    k, max_iter = 0, 5
    # MUMPS Documentation - infog(1) = -9
    # The main internal real/complex workarray S is too small. If INFO(2) is positive, then the number
    # of entries that are missing in S at the moment when the error is raised is available in INFO(2).
    # If INFO(2) is negative, then its absolute value should be multiplied by 1 million. If an error –9
    # occurs, the user should increase the value of ICNTL(14) before calling the factorization (JOB=
    # 2) again, except if LWK USER is provided LWK USER should be increased.
    while mumps.infog[1] == -9 && k < max_iter
      MUMPS.set_icntl!(mumps, 14, mumps.icntl[14] * 2)
      mumps.job = MUMPS.FACTOR
      factorize!(mumps)
      workspace._n_fact += 1
      k = k + 1
    end

    # MUMPS infog(1): a negative value is an error in the factorization.
    if mumps.infog[1] < 0
      workspace.status = :failed
      return
    else
      workspace.factorized = true
    end
  end

  # Step 2: Compute
  # [x₁] = [σI+ξI  Aᵀ]⁻¹[u]
  # [x₁] = [A     -αI]  [u]

  x1 .= u
  MUMPS.associate_rhs!(mumps, x1)
  MUMPS.mumps_solve!(x1, mumps; rhs_changed = true)

  # MUMPS infog(1): a negative value is an error in the factorization.
  if any(isnan, x1) || mumps.infog[1] < 0
    workspace.status = :failed
    return
  end

  if mumps.rinfog[6] > sqrt(eps(eltype(workspace.x))) ||
     mumps.rinfog[7] > sqrt(eps(eltype(workspace.x))) ||
     mumps.rinfog[8] > sqrt(eps(eltype(workspace.x)))
    workspace.status = :failed

    # Switch to symmetric indefinite factorization
    mumps_switch_to_indefinite!(workspace)

    return
  end

  # Step 3: Compute
  # y₁ = Fᵀx₁ = [Uᵀx₁(1:n)]
  # y₁ = Fᵀx₁ = [Vᵀx₁(1:n)]
  @views mul!(y1[1:p], Uk', x1[1:n])
  @views mul!(y1[(p+1):(2*p)], Vk', x1[1:n])


  # Step 4: Assemble Schur complement (I + Fᵀ [σI+ξI  Aᵀ]⁻¹ E )
  #                                   (       [A     -αI]     )
  # Step 4.1: Compute 
  # Z₁ = [σI+ξI  Aᵀ]⁻¹ E = [σI+ξI  Aᵀ]⁻¹[-U V]
  # Z₁ = [A     -αI]   E = [A     -αI]  [ 0 0]
  Z1 .= 0

  @views Z1[1:n, 1:p] .= Uk .* (-1)
  @views Z1[1:n, (p+1):(2*p)] .= Vk

  MUMPS.associate_rhs!(mumps, Z1)
  MUMPS.mumps_solve!(Z1, mumps; rhs_changed = true)

  # MUMPS infog(1): a negative value is an error in the factorization.
  if any(isnan, Z1) || mumps.infog[1] < 0
    workspace.status = :failed
    return
  end

  if mumps.rinfog[6] > sqrt(eps(eltype(workspace.x))) ||
     mumps.rinfog[7] > sqrt(eps(eltype(workspace.x))) ||
     mumps.rinfog[8] > sqrt(eps(eltype(workspace.x)))
    workspace.status = :failed

    # Switch to symmetric indefinite factorization
    mumps_switch_to_indefinite!(workspace)
    return
  end

  # Step 4.2: Compute 
  # Z₂ = FᵀZ₁ = UᵀZ₁[1:n]
  # Z₂ = FᵀZ₁ = VᵀZ₁[1:n]
  Z2 .= 0
  @views mul!(Z2[1:p, 1:(2*p)], Uk', Z1[1:n, (1:(2*p))])
  @views mul!(Z2[(p+1):(2*p), 1:(2*p)], Vk', Z1[1:n, (1:(2*p))])

  # Step 4.3: Compute 
  # Z₂ = I + Z₂
  for i = 1:(2*p)
    Z2[i, i] += 1
  end

  # Step 5: Solve
  # (I + Fᵀ [σI+ξI  Aᵀ]⁻¹ E )⁻¹[y₁]
  # (       [A     -αI]     )  [y₁]
  # using Julia LinearALgebra's lu!
  F = lu!(Z2[1:(2*p), 1:(2*p)], check = false) # FIXME ?
  @views ldiv!(y2[1:(2*p)], F, y1[1:(2*p)])
  if any(isnan, y2)
    workspace.status = :failed
    return
  end

  # Step 6: Compute
  # x₂ = E[y₂] = [-U V][y₂] = [-Uy₂ + Vy₂]
  # x₂ = E[y₂] = [ 0 0][y₂] = [0]
  @views mul!(x2[1:n], Vk, y2[(p+1):(2*p)])
  @views mul!(x2[1:n], Uk, y2[1:p], -one(eltype(y2)), one(eltype(y2)))

  # Step 7: Solve
  # [x₃] = [σI+ξI  Aᵀ]⁻¹[x₂]
  # [x₃] = [A     -αI]  [x₂]
  x3 .= x2
  MUMPS.associate_rhs!(mumps, x3)
  MUMPS.mumps_solve!(x3, mumps; rhs_changed = true)

  # MUMPS infog(1): a negative value is an error in the factorization.
  if any(isnan, x3) || mumps.infog[1] < 0
    workspace.status = :failed
    return
  end

  if mumps.rinfog[6] > sqrt(eps(eltype(workspace.x))) ||
     mumps.rinfog[7] > sqrt(eps(eltype(workspace.x))) ||
     mumps.rinfog[8] > sqrt(eps(eltype(workspace.x)))
    workspace.status = :failed

    # Switch to symmetric indefinite factorization
    mumps_switch_to_indefinite!(workspace)

    return
  end

  # Step 8:
  # [B  Aᵀ]⁻¹[u] = x₁ - x₃ 
  # [A -αI]  [u] = x₁ - x₃
  workspace.x .= x1 .- x3
end

function get_solution!(x::V, workspace::PenaltyMUMPSWorkspace) where {V<:AbstractVector}
  x .= workspace.x
end

function get_status(workspace::PenaltyMUMPSWorkspace)
  return workspace.status
end

function get_inertia(workspace::PenaltyMUMPSWorkspace{WP,K2}) where {WP,K2}

  n, m = workspace.n, workspace.m
  (npos, nzero, nneg) = (0, 0, 0)

  nneg = workspace.M.infog[12]
  rank = n + m - workspace.M.infog[28]
  nzero = n + m - rank
  npos = n + m - nzero - nneg

  return npos, nzero, nneg
end

function mumps_switch_to_indefinite!(workspace::PenaltyMUMPSWorkspace)
  mumps = workspace.M
  H, x = get_H(workspace), workspace.x
  n, m = workspace.n, workspace.m
  mumps.sym == 2 && return

  mumps.sym = 2
  mumps.job = MUMPS.INITIALIZE
  MUMPS.invoke_mumps_unsafe!(mumps)

  # Associate the row, cols and vals of the mumps structure with those of H.
  irn, jcn, a = H.rows, H.cols, H.vals
  mumps.irn, mumps.jcn, mumps.a = pointer.((irn, jcn, a))
  mumps.n = m+n
  mumps.nnz = length(irn)
  mumps._irn_gc_haven = irn
  mumps._jcn_gc_haven = jcn
  mumps._a_gc_haven = a

  # Associate the size and number of the right hand side
  mumps.lrhs = n + m
  mumps.nrhs = 1
  mumps.rhs = pointer(x)
  mumps._y_gc_haven = x

  icntl = mumps.icntl

  # Deactivate Logging
  redirect_stdout(devnull) do
    MUMPS.set_icntl!(mumps, 2, 0)
    MUMPS.set_icntl!(mumps, 3, 0)
    MUMPS.set_icntl!(mumps, 4, 0)
  end

  # Max number of iterative refinement steps
  MUMPS.set_icntl!(mumps, 10, -10)

  # See `construct_mumps_workspace` for more details on these parameters
  MUMPS.set_icntl!(mumps, 24, 1)
  MUMPS.set_icntl!(mumps, 11, 2)

  MUMPS.set_cntl!(mumps, 1, 1e-1)
  MUMPS.set_cntl!(mumps, 2, eps(eltype(x)))
end
